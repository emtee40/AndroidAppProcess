plugins {
    alias(libs.plugins.agp.lib)
    id "maven-publish"
    id "signing"
}

def ossrhUsername = ''
def ossrhPassword = ''
def extGradlePath = rootDir.path + "/keystore/r0s.gradle"
if (file(extGradlePath).exists()) {
    apply from: extGradlePath
}

android {
    namespace = "com.rosan.app_process"
    compileSdk = 35

    defaultConfig {
        minSdk = 19
    }

    buildFeatures {
        buildConfig = true
        aidl = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    compileOnly project(":hidden-api")
    compileOnly(libs.annotation)

    implementation(libs.commons.cli)
    implementation(libs.hiddenapibypass)
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = rootProject.groupId
            artifactId = rootProject.artifactId
            version = rootProject.artifactVersion
            afterEvaluate {
                from components.findByName("release")
            }
            pom {
                name = rootProject.POM_NAME
                description = rootProject.POM_DESCRIPTION
                url = rootProject.POM_URL
                licenses {
                    license {
                        name = rootProject.POM_LICENCE_NAME
                        url = rootProject.POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        name = rootProject.POM_DEVELOPER_NAME
                        url = rootProject.POM_DEVELOPER_URL
                    }
                }
                scm {
                    connection = rootProject.POM_SCM_CONNECTION
                    url = rootProject.POM_SCM_URL
                }
            }
        }
    }
    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
