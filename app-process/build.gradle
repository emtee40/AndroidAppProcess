plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

def extGradlePath = rootDir.path + "/keystore/r0s.gradle"
if (file(extGradlePath).exists()) {
    apply from: extGradlePath
}

android {
    namespace 'com.rosan.app_process'
    compileSdk 33

    defaultConfig {
        minSdk 19
    }

    buildFeatures {
        aidl true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.6.0'
    compileOnly project(':hidden-api')
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group rootProject.groupId
            artifactId rootProject.artifactId
            version rootProject.artifactVersion
            afterEvaluate {
                from components.release
            }
            pom {
                name = rootProject.POM_NAME
                description = rootProject.POM_DESCRIPTION
                url = rootProject.POM_URL
                licenses {
                    license {
                        name = rootProject.POM_LICENCE_NAME
                        url = rootProject.POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        name = rootProject.POM_DEVELOPER_NAME
                        url = rootProject.POM_DEVELOPER_URL
                    }
                }
                scm {
                    connection = rootProject.POM_SCM_CONNECTION
                    url = rootProject.POM_SCM_URL
                }
            }
        }
    }
    repositories {
        maven {
            name 'ossrh'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}